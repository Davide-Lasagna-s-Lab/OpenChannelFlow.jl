# Symmetry operators on channel fields.

# TODO: add discrete symmetries: σ₁[u,v,w](x,y,z)->[u,v,-w](x,y,-z), σ₂[u,v,w](x,y,z)->[-u,-v,w](-x,-y,z)
# see: Visualizing the geometry of state space in plane Couette flow, Gibson, Halcrow, Cvitanovic, section 2.2

"""
    shift!(u::F, shifts::NTuple{3, Real}) -> F

Return the channel field `u` shifted in streamwise, spanwise, and temporal
directions.

Apply a continuous shift to the field `u` in the streamwise, spanwise, and
temporal directions, given by `shifts[1]`, `shifts[2]`, and `shifts[3]`
respectively. This function modifies the field it is provided, so be careful
when using.
"""
shift!(u::Union{SCField, VectorField, ProjectedField}, shifts) = tshift!(zshift!(xshift!(u, shifts[1]), shifts[2]), shifts[3])


# ---------------- #
# streamwise shift #
# ---------------- #
xshift!(u::SCField{G}, sx) where {S, G<:ChannelGrid{S}} = _perform_xshift!(u, sx, S[1], S[2], S[3], S[4])
xshift!(a::ProjectedField{G, M}, sx) where {S, G<:ChannelGrid{S}, M} = _perform_xshift!(a, sx, M, S[2], S[3], S[4])

function xshift!(u::VectorField{N, <:SCField}, sx) where {N}
    for n in 1:N
        xshift!(u[n], sx)
    end
    return u
end

function _perform_xshift!(field, sx, S1, S2, S3, S4)
    sx == 0 && return field
    for nx in 0:(S2 >> 1)
        val = cis(nx*sx*grid(field).α)
        for nt in -(S4 >> 1):(S4 >> 1), nz in -(S3 >> 1):(S3 >> 1), m in 1:S1
            @inbounds field[m, ModeNumber(nx, nz, nt)] *= val
        end
    end
    return field
end

# -------------- #
# spanwise shift #
# -------------- #
zshift!(u::SCField{G}, sz) where {S, G<:ChannelGrid{S}} = _perform_zshift!(u, sz, S[1], S[2], S[3], S[4])
zshift!(a::ProjectedField{G, M}, sz) where {S, G<:ChannelGrid{S}, M} = _perform_zshift!(a, sz, M, S[2], S[3], S[4])

function zshift!(u::VectorField{N, <:SCField}, sz) where {N}
    for n in 1:N
        zshift!(u[n], sz)
    end
    return u
end

function _perform_zshift!(field, sz, S1, S2, S3, S4)
    sz == 0 && return field
    for nz in -(S3 >> 1):(S3 >> 1)
        val = cis(nz*sz*grid(field).β)
        for nt in -(S4 >> 1):(S4 >> 1), nx in 0:(S2 >> 1), m in 1:S1
            @inbounds field[m, ModeNumber(nx, nz, nt)] *= val
        end
    end
    return field
end


# -------------- #
# temporal shift #
# -------------- #
tshift!(u::SCField{G}, st) where {S, G<:ChannelGrid{S}} = _perform_tshift!(u, st, S[1], S[2], S[3], S[4])
tshift!(a::ProjectedField{G, M}, st) where {S, G<:ChannelGrid{S}, M} = _perform_tshift!(a, st, M, S[2], S[3], S[4])

function tshift!(u::VectorField{N, <:SCField}, st) where {N}
    for n in 1:N
        tshift!(u[n], st)
    end
    return u
end

function _perform_tshift!(field, st, S1, S2, S3, S4)
    st == 0 && return field
    for nt in -(S4 >> 1):(S4 >> 1)
        val = cispi(2*nt*st)
        for nz in -(S3 >> 1):(S3 >> 1), nx in 0:(S2 >> 1), m in 1:S1
            @inbounds field[m, ModeNumber(nx, nz, nt)] *= val
        end
    end
    return field
end
